	SUBROUTINE GD_4010_CONVERT(IX,IY)
C
C	CONVERTS (IX,IY) TO THE 4010 10-BIT FORMAT AND PLACES THE
C	CHARACTERS INTO THE BUFFER.   OPTIMIZED FOR MINIMUM CHARS TO BE
C	TRANSMITTED.
C
	COMMON /GD4010STATEMEM/ IOLD_HIY,IOLD_LOY,IOLD_HIX
	DATA IOLD_HIY, IOLD_LOY, IOLD_HIX /3*-1/
	IHIY = 32+IY/32
	ILOY = 96+IAND(IY,31)
	IHIX = 32+IX/32
C
C	HI-Y ONLY NEEDS BE SENT WHEN IT CHANGES
C
	IF (IHIY .NE. IOLD_HIY) THEN
		IOLD_HIY = IHIY
		CALL GB_INSERT(IHIY)
	ENDIF
C
C	SEND LO-Y IF IT CHANGED OR IF WE NEED TO SEND A HI-X
C
	IF (ILOY .NE. IOLD_LOY .OR.
	1   IHIX .NE. IOLD_HIX) THEN
		IOLD_LOY = ILOY
		CALL GB_INSERT(ILOY)
	ENDIF
C
C	HI-X CAN ONLY BE SENT IF PRECEEDED BY LO-Y --> THIS IS HANDLED
C	PREVIOUSLY.
C
	IF (IHIX .NE. IOLD_HIX) THEN
		IOLD_HIX = IHIX
		CALL GB_INSERT(IHIX)
	ENDIF
C
C	LO-X MUST ALWAYS BE SENT
C
	CALL GB_INSERT(64+IAND(IX,31))
	RETURN
	END
C
C
C
	SUBROUTINE GD_4010_CAN
	COMMON /GD4010STATEMEM/ IOLD_HIY,IOLD_LOY,IOLD_HIX
	IOLD_HIY = -1
	IOLD_LOY = -1
	IOLD_HIX = -1
	RETURN
	END
