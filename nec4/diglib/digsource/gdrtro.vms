	SUBROUTINE GDRTRO(IFXN,XA,YA)
	DIMENSION XA(8), YA(3)
C
C	VT100 WITH 640x480 RETROGRAPHICS DRIVER FOR DIGLIB/VAX
C
C-----------------------------------------------------------------------
C
	EXTERNAL LEN
	BYTE ESC, CSUB, TMODE, GS, CR, FF
	PARAMETER (ESC=27, CSUB=26, TMODE=24, GS=29, CR=13, FF=12)
	CHARACTER*(*) TERMINAL
	PARAMETER (TERMINAL='TT')
C
C	DEFINITIONS FOR DEVICE CONTROL
C
	BYTE STR_BEGIN_PLOT(14), STR_COLOR_SET(6)
	BYTE STR_END_PLOT(8)
	DATA STR_BEGIN_PLOT /ESC,'[','H',ESC,'[','J',GS,ESC,FF,
	1   ESC,'/','0','d',0/
	DATA STR_COLOR_SET /GS,ESC,'/','0','d',0/
	DATA STR_END_PLOT /ESC,'[','H',ESC,'[','J',0,0/
C
C	DEFINITIONS FOR GIN
C
	BYTE GINBUFR(8), PROMPT(4)
	DATA PROMPT /GS, ESC, CSUB, 0/
	DATA IGIN_IN_CHARS /6/		!5 FROM 4010 GIN, PLUS CR
C
C	DECLARE BUFFERING FUNCTION
C
	LOGICAL GB_TEST_FLUSH
C
C	DECLARE VARS NEED FOR DRIVER OPERATION
C
	LOGICAL LVECTOR_GOING, LDUMMY
	DIMENSION DCHAR(8)
C
C	MAKE NICE NAMES FOR THE DEVICES RESOLUTION IN X AND Y
C	 ("XGUPCM" IS X GRAPHICS UNITS PER CENTIMETER)
C
	EQUIVALENCE (DCHAR(4),XGUPCM), (DCHAR(5),YGUPCM)
	DATA DCHAR /100.0, 21.001, 15.747, 30.419, 30.419, 1.0, 133.0, 1.0/
C
C*****************
C
C	FIRST VERIFY WE GOT A GRAPHICS FUNCTION WE CAN HANDLE
C
	IF (IFXN .LE. 0 .OR. IFXN .GT. 9) RETURN
C
C	NOW DISPATCH TO THE PROPER CODE TO HANDLE THAT FUNCTION
C
	GO TO (100,200,300,400,500,600,700,800,900) IFXN
C
C	*********************
C	INITIALIZE THE DEVICE
C	*********************
C
100	CONTINUE
C
C	FIRST, INITIALIZE THE BUFFER SUBROUTINES
C
	CALL GB_INITIALIZE(TMODE,0,TERMINAL,IERR)
	YA(1) = IERR
	LVECTOR_GOING = .FALSE.
	RETURN
C
C	**************************
C	GET FRESH PLOTTING SURFACE
C	**************************
C
200	CONTINUE
	CALL GB_EMPTY
	CALL GB_IN_STRING(STR_BEGIN_PLOT)
	CALL GB_USE_TERMINATOR
	LVECTOR_GOING = .FALSE.
	RETURN
C
C	****
C	MOVE
C	****
C
300	CONTINUE
C	CONVERT CM. TO GRAPHICS UNITS ROUNDED
	IXPOSN = XGUPCM*XA(1)+0.5
	IYPOSN = YGUPCM*YA(1)+0.5
	LVECTOR_GOING = .FALSE.
	RETURN
C
C	****
C	DRAW
C	****
C
400	CONTINUE
	IX = XGUPCM*XA(1)+0.5
	IY = YGUPCM*YA(1)+0.5
	LVECTOR_GOING = LVECTOR_GOING .AND. (.NOT. GB_TEST_FLUSH(4))
	IF (LVECTOR_GOING) GO TO 410
	LDUMMY = GB_TEST_FLUSH(9)
	LVECTOR_GOING = .TRUE.
	CALL GB_INSERT(GS)
	CALL GB_USE_TERMINATOR
	CALL GD_4010_CONVERT((8*IXPOSN/5),(13*IYPOSN)/8)
410	CALL GD_4010_CONVERT((8*IX/5),(13*IY)/8)
	IXPOSN = IX
	IYPOSN = IY
	RETURN
C
C	*****************************
C	FLUSH GRAPHICS COMMAND BUFFER
C	*****************************
C
500	CONTINUE
	CALL GB_EMPTY
	CALL GB_IN_STRING(STR_END_PLOT)
	CALL GB_EMPTY
	LVECTOR_GOING = .FALSE.
	RETURN
C
C	******************
C	RELEASE THE DEVICE
C	******************
C
600	CONTINUE
C
C	DE-ASSIGN THE CHANNAL
C
	CALL GB_FINISH(0)
	RETURN
C
C	*****************************
C	RETURN DEVICE CHARACTERISTICS
C	*****************************
C
700	CONTINUE
	DO 720 I=1,8
	XA(I) = DCHAR(I)
720	CONTINUE
	RETURN
C
C	****************************
C	SELECT CURRENT DRAWING COLOR
C	****************************
C
800	CONTINUE
	LDUMMY = GB_TEST_FLUSH(8)
	ICOLOR = XA(1)
	IF (ICOLOR .LT. 0 .OR. ICOLOR .GT. 1) RETURN
	ICOLOR = 1-ICOLOR		!CONVERT 1 TO 0 AND 0 INTO 1
	STR_COLOR_SET(4) = 48+ICOLOR	!MAKE ASCII CHARACTER 0 OR 1
	CALL GB_IN_STRING(STR_COLOR_SET)
	CALL GB_USE_TERMINATOR
	LVECTOR_GOING = .FALSE.
	RETURN
C
C	**********************
C	PERFORM GRAPHICS INPUT
C	**********************
C
900	CONTINUE
	CALL GB_EMPTY
	LVECTOR_GOING = .FALSE.
C
C	IT IS COMPLETELY UNCLEAR WHY THIS DELAY IS REQUIRED IN SOME CASES BY
C	THE RETRO-GRAPHICS, BUT IT SEEMS TO BE.
C
	CALL GDWAIT(400)
C
	CALL GB_GIN(PROMPT,IGIN_IN_CHARS,.TRUE.,GINBUFR)
C
	ICHAR = GINBUFR(1)
	IX1 = GINBUFR(2)
	IX2 = GINBUFR(3)
	IY1 = GINBUFR(4)
	IY2 = GINBUFR(5)
C
	XA(1) = IAND(ICHAR,127)		!PICK CHARACTER
	XA(2) = (5.0*(32*IAND(IX1,31)+IAND(IX2,31))/8.0)/XGUPCM
	XA(3) = (8.0*(32*IAND(IY1,31)+IAND(IY2,31))/13.0)/YGUPCM
C
	CALL GB_SEND_CHARS(TMODE,1)
	RETURN
	END
