	SUBROUTINE GDZETA1453_TALL(IFXN,XA,YA)
	DIMENSION XA(8), YA(3)
C
C	DIGLIB ZETA 8 GRAPHICS DEVICE DRIVER
C	 USES THE ZETA "FUNDAMENTAL PLOTTING SUBROUTINES"
C
C-----------------------------------------------------------------------
C
C	CHANGE HISTORY
C	ROBERT M SHECTMAN			29 MAY 1986
C
C	TAILOR THE GENERAL ZETA DRIVER FOR THE 1453.  ONLY 4 PENS,
c	AND FORGET THAT STUFF ABOUT 8 X 10 AND A BORDER, USE THE
C	ENTIRE PAPER IF YOU CAN.  IN ADDITION SET UP THE PAPER SIZE
C	TO BE 11 X 17
C
C	-------------------------------------------------------------------
C
	DIMENSION DCHAR(8)
	LOGICAL*2 LWIDE
C
C	THE ZETA 8 IS ASSUMED TO BE SET FOR RESOLUTION OF 0.025 mm
C	DIGLIB ASSUMES 8.5 INCH FAN FOLD PAPER.   DIGLIB USES A PLOTTING
C	 SURFACE OF 8X10 INCHES, WITH EQUAL 0.25 INCH BORDERS IN THE X
C	 DIRECTION, A BOTTOM BORDER OF 0.25 INCH, AND A TOP BORDER OF
C	 0.75 INCH.   THUS THE DIGLIB PLOTTING SURFACE OF 8X10 IS PLACED
C	 NICELY ON 8.5X11.0 INCH PAPER.
C	THIS DIGLIB DRIVER PROVIDES AN ALTERNATE ENTRY POINT FOR ROTATING
C	 THE PLOT 90 DEGREES WHEN THE USER WANTS A PLOT THAT IS WIDER THAN
C	 IT IS TALL.   THE ENTRY POINT NAME IS "GDZETA8WIDE".   THE SAME
C	 BOTTOM AND LEFT BORDERS ARE USED.
C
	PARAMETER (CM_PER_INCH = 2.54)
C-----------------------------------------------------------------------
C
C PAPER DEFINITIONS - ALL IN INCHES
C
	PARAMETER (PAPER_WIDTH = 17.0)	!PAPER FAN FOLD WIDTH
	PARAMETER (PAPER_HEIGHT = 11.0)	!PAPER HEIGHT
	PARAMETER (LEFT_BORDER = 0.25)	!LEFT SIDE BORDER
	PARAMETER (BOTTOM_BORDER = 0.25)!BOTTOM OF PAPER BORDER
	PARAMETER (PLOT_WIDTH = 16.5)	!WIDTH OF PAPER USED FOR PLOTTING
	PARAMETER (PLOT_HEIGHT = 10.5)	!HEIGHT OF PAPER USE FOR PLOTTING
C
C PLOTTER DEFINITIONS - ALL IN CENTIMETERS
C
	PARAMETER (RESOLUTION = 1.0/0.0025)	!RESOLUTION
	PARAMETER (PEN_WIDTH = 0.002)	!PEN LINE WIDTH
C
C***********************************************************************
C
C CALCULATED QUANTITIES FOR PLOTTER
C
	PARAMETER (X_WIDE = CM_PER_INCH*PLOT_WIDTH)
	PARAMETER (Y_HIGH = CM_PER_INCH*PLOT_HEIGHT)
	PARAMETER (SKIPPED_LINES = PEN_WIDTH*RESOLUTION)
C
C***********************************************************************
C
	DATA DCHAR /4.0, X_WIDE, Y_HIGH, RESOLUTION, RESOLUTION,
	1   7.0, 3.0, SKIPPED_LINES/
C
C	SHOW WE WANT TALL NOT WIDE PLOTTING AREA
C
	LWIDE = .FALSE.
10	CONTINUE
C
C	FIRST VERIFY WE GOT A GRAPHICS FUNCTION WE CAN HANDLE
C
	IF (IFXN .LE. 0 .OR. IFXN .GT. 8) RETURN
C
C	NOW DISPATCH TO THE PROPER CODE TO HANDLE THAT FUNCTION
C
	GO TO (100,200,300,400,500,600,700,800) IFXN
C
C	*********************
C	INITIALIZE THE DEVICE
C	*********************
C
100	CONTINUE
	OPEN(UNIT=10,FILE='ZETAPLOT:ZETAOUT.SMALL',STATUS='NEW')
	CALL PLOTS(20,IDUMMY,10)
	YA(1) = 0.0
	RETURN
C
C	**************************
C	GET FRESH PLOTTING SURFACE
C	**************************
C
200	CONTINUE
	CALL NEWPEN(1)
	CALL PLOT(PAPER_WIDTH,0.0,-3)
	RETURN
C
C	****
C	MOVE
C	****
C
300	CONTINUE
	IPEN = +3
	GO TO 450
C
C	****
C	DRAW
C	****
C
400	CONTINUE
	IPEN = +2
450	CONTINUE
C
C	ZETA "PLOT" SUBROUTINE WANTS INCHES, SO CONVERT
C
	X = XA(1)/CM_PER_INCH
	Y = YA(1)/CM_PER_INCH
	IF (LWIDE) THEN
		CALL PLOT(LEFT_BORDER+PLOT_WIDTH-Y,BOTTOM_BORDER+X,IPEN)
	    ELSE
		CALL PLOT(LEFT_BORDER+X,BOTTOM_BORDER+Y,IPEN)
	END IF
	RETURN
C
C	*****************************
C	FLUSH GRAPHICS COMMAND BUFFER
C	*****************************
C
500	CONTINUE
C
C	NOP FOR ZETA 8 CAUSE I DON'T KNOW HOW TO MAKE THE FUNDAMENTAL
C	 PLOTTING ROUTINES DO IT
C
	RETURN
C
C	******************
C	RELEASE THE DEVICE
C	******************
C
600	CONTINUE
	CALL PLOT(PAPER_WIDTH, 0.0, +999)
	RETURN
C
C	*****************************
C	RETURN DEVICE CHARACTERISTICS
C	*****************************
C
700	CONTINUE
	DO 720 I=1,8
	XA(I) = DCHAR(I)
720	CONTINUE
	IF (.NOT. LWIDE) RETURN
	XA(2) = DCHAR(3)
	XA(3) = DCHAR(2)
	RETURN
C
C	SELECT NEW COLOR
C
800	CONTINUE
	CALL NEWPEN(INT(XA(1)))
	RETURN
C
C	ALTERNATE ENTRY FOR WIDE PLOTTING AREA
C
	ENTRY GDZETA1453_WIDE(IFXN,XA,YA)
	LWIDE = .TRUE.
	GO TO 10
	END
