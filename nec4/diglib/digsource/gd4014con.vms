	SUBROUTINE GD_4014_CONVERT(IX,IY)
C
C	CONVERTS (IX,IY) TO THE 4014 12-BIT FORMAT AND PLACES THE
C	CHARACTERS INTO THE BUFFER.   OPTIMIZED FOR MINIMUM CHARS TO BE
C	TRANSMITTED.
C
	COMMON /GD_4014_STATE_MEM/ IOLD_HIY, IOLD_EX, IOLD_LOY, IOLD_HIX
	DATA IOLD_HIY, IOLD_EX, IOLD_LOY, IOLD_HIX /4*-1/
	IHIY = 32+IY/128
	IEX = 96+4*IAND(IY,3)+IAND(IX,3)
	ILOY = 96+IAND(IY/4,31)
	IHIX = 32+IX/128
C
C	HI-Y ONLY NEEDS BE SENT WHEN IT CHANGES
C
	IF (IHIY .NE. IOLD_HIY) THEN
		IOLD_HIY = IHIY
		CALL GB_INSERT(IHIY)
	ENDIF
C
C	EXTRA-BITS ONLY NEEDS BE SENT WHEN IT CHANGES, BUT IF SENT, THEN
C	LO-Y MUST BE SENT EVEN IF IT DIDN'T CHANGE.
C
	IF (IEX .NE. IOLD_EX) THEN
		IOLD_EX = IEX
		CALL GB_INSERT(IEX)
		CALL GB_INSERT(ILOY)
		IOLD_LOY = ILOY
	    ELSE
C
C		SEND LO-Y IF IT CHANGED OR IF WE NEED TO SEND A HI-X
C
		IF (ILOY .NE. IOLD_LOY .OR.
	1	   IHIX .NE. IOLD_HIX) THEN
			IOLD_LOY = ILOY
			CALL GB_INSERT(ILOY)
		ENDIF
	ENDIF
C
C	HI-X CAN ONLY BE SENT IF PRECEEDED BY LO-Y --> THIS IS HANDLED
C	PREVIOUSLY.
C
	IF (IHIX .NE. IOLD_HIX) THEN
		IOLD_HIX = IHIX
		CALL GB_INSERT(IHIX)
	ENDIF
C
C	LO-X MUST ALWAYS BE SENT
C
	CALL GB_INSERT(64+IAND(IX/4,31))
	RETURN
	END

	SUBROUTINE GD_4014_ZORCH
	COMMON /GD_4014_STATE_MEM/ IOLD_HIY, IOLD_EX, IOLD_LOY, IOLD_HIX
	IOLD_HIY = -1
	IOLD_EX = -1
	IOLD_LOY = -1
	IOLD_HIX = -1
	RETURN
	END
