	SUBROUTINE GD4CON(IX,IY)
C
C	CONVERTS (IX,IY) TO THE 4010 10-BIT FORMAT AND PLACES THE
C	CHARACTERS INTO THE BUFFER.   OPTIMIZED FOR MINIMUM CHARS TO BE
C	TRANSMITTED.
C
	COMMON /GD4010/ IOHIY,IOLOY,IOHIX
	IHIY = 32+IY/32
	ILOY = 96+IAND(IY,31)
	IHIX = 32+IX/32
C
C	HI-Y ONLY NEEDS BE SENT WHEN IT CHANGES
C
	IF (IHIY .NE. IOHIY) THEN
		IOHIY = IHIY
		CALL GBINSE(char(IHIY))
	ENDIF
C
C	SEND LO-Y IF IT CHANGED OR IF WE NEED TO SEND A HI-X
C
	IF (ILOY .NE. IOLOY .OR.
     1   IHIX .NE. IOHIX) THEN
		IOLOY = ILOY
		CALL GBINSE(char(ILOY))
	ENDIF
C
C	HI-X CAN ONLY BE SENT IF PRECEEDED BY LO-Y --> THIS IS HANDLED
C	PREVIOUSLY.
C
	IF (IHIX .NE. IOHIX) THEN
		IOHIX = IHIX
		CALL GBINSE(char(IHIX))
	ENDIF
C
C	LO-X MUST ALWAYS BE SENT
C
	CALL GBINSE(char(64+IAND(IX,31)))
	RETURN
	END
C
C
C
	SUBROUTINE GD4CAN
	COMMON /GD4010/ IOHIY,IOLOY,IOHIX
	IOHIY = -1
	IOLOY = -1
	IOHIX = -1
	RETURN
	END
